buildscript {
  repositories {
    jcenter()
  }
}

plugins {
  id "com.jfrog.bintray" version "1.7.1"
}

configure(allprojects) {
  group = 'org.openmuc.jasn1'
  version = '1.1.0'

  apply plugin: 'java'

  repositories {
    jcenter()
    // TODO remove the line, dependencies are supposed to be linked in jcenter
    maven {
      url 'http://dl.bintray.com/content/juherr/asn1'
    }
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
  }
}

configure(subprojects) {

  apply plugin: 'osgi'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'

  sourceCompatibility = 1.5
  targetCompatibility = 1.5

  dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.+'
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        artifact sourceJar {
          classifier "sources"
        }

        artifact javadocJar {
          classifier "javadoc"
        }

        pom.withXml {
          def root = asNode();
          root.appendNode('url', 'http://www.openmuc.org/')
          def github = root.appendNode('issueManagement')
          github.appendNode('system', 'github')
          github.appendNode('url', 'https://github.com/juherr/jASN1/issues')
          def scm = root.appendNode('scm')
          scm.appendNode('url', 'https://github.com/juherr/jASN1/')
          scm.appendNode('connection', 'scm:git:https://github.com/juherr/jASN1.git')
          scm.appendNode('developerConnection', 'scm:git:https://github.com/juherr/jASN1.git')
          def license = root.appendNode('licenses').appendNode('license');
          license.appendNode('name', 'GNU Lesser General Public License')
          license.appendNode('url', 'http://www.gnu.org/licenses/lgpl-3.0.html')
          license.appendNode('distribution', 'repo')
        }
      }
    }
    repositories {
      maven {
        name 'bintray-juherr-asn1-jASN1'
        url 'https://api.bintray.com/maven/juherr/asn1/jASN1/'
        credentials {
          username = System.getenv('BINTRAY_USER')
          password = System.getenv('BINTRAY_API_KEY')
        }
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}
