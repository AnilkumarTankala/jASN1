
configure(allprojects) {
  version = '1.0.1'

  apply plugin: 'java'

  repositories { mavenCentral()  }

}

configure(subprojects) {

  apply plugin: 'eclipse'
  apply plugin: 'osgi'
  apply plugin: 'maven'

  sourceCompatibility = 1.5
  targetCompatibility = 1.5

  dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.+'
  }

  task delLibs(type: Delete) { delete 'lib/' }

  task copyLibs(dependsOn: configurations.default.buildArtifacts, type: Copy) {
    into "lib"
    from configurations.default
    from configurations.default.allArtifactFiles
  }

  task delJavadoc(type: Delete) { delete 'doc/javadoc/' }

  task copyJavadoc(type: Copy) {
    dependsOn(javadoc)

    into "doc"
    from "build/docs/"
  }

  clean {
    dependsOn(delLibs)
    dependsOn(delJavadoc)
  }

  eclipseClasspath.variables = [GRADLE_HOME:file(gradle.gradleUserHomeDir)]
  eclipse.dependsOn(cleanEclipse)
}


task (tar, type: Tar) {
  dependsOn('jasn1-compiler:copyLibs')
  dependsOn('jasn1-ber:copyLibs')
  dependsOn('jasn1-compiler:copyJavadoc')
  dependsOn('jasn1-ber:copyJavadoc')

  compression = Compression.GZIP

  into(project.archivesBaseName) {
    from('./') {
      include 'jasn1-ber/src/**/*'
      include 'jasn1-ber/lib/**/*'
      include 'jasn1-ber/doc/**/*'
      include 'jasn1-compiler/src/**/*'
      include 'jasn1-compiler/grammatic/**/*'
      include 'jasn1-compiler/lib/**/*'
      include 'jasn1-compiler/doc/**/*'
      include 'jasn1-compiler/build.gradle'
      include 'jasn1-compiler/dependencies/**/*'
      include 'doc/**/*'
      include 'licenses/**/*'
      include 'sample/**/*.java'
      include 'sample/**/*.txt'
      include 'sample/**/*.asn'
      include 'build.gradle'
      include 'settings.gradle'
    }
    from('./') {
      fileMode = 0755
      include 'jasn1-compiler/run-scripts/**/*'
    }
  }

}

task wrapper(type: Wrapper) {
  gradleVersion = '0.9.2'
}
