buildscript {
  repositories {
    jcenter()
  }
}

plugins {
  id "com.jfrog.bintray" version "1.7.1"
}

configure(allprojects) {
  apply plugin: 'java'
  version = '1.2.1'
  group = 'org.openmuc.jasn1'
}

configure(subprojects) {

  apply plugin: 'osgi'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'

  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  repositories {
    jcenter()
    // TODO remove the line, dependencies are supposed to be linked in jcenter
    maven {
      url 'http://dl.bintray.com/content/juherr/asn1'
    }
  }

  dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java

        artifact javadocJar {
          classifier "javadoc"
        }

        pom.withXml {
          def root = asNode();
          root.appendNode('url', 'http://www.openmuc.org/')
          root.appendNode('description', 'jasn1 is a compiler that generates Java classes out of ASN.1 code that can be used to encode/decode BER data.')
          def github = root.appendNode('issueManagement')
          github.appendNode('system', 'github')
          github.appendNode('url', 'https://github.com/juherr/jASN1/issues')
          def scm = root.appendNode('scm')
          scm.appendNode('url', 'https://github.com/juherr/jASN1/')
          scm.appendNode('connection', 'scm:git:https://github.com/juherr/jASN1.git')
          scm.appendNode('developerConnection', 'scm:git:https://github.com/juherr/jASN1.git')
          def license = root.appendNode('licenses').appendNode('license');
          license.appendNode('name', 'GNU General Public License')
          license.appendNode('url', 'http://www.gnu.org/licenses/gpl-3.html')
          license.appendNode('distribution', 'repo')
          def developer = root.appendNode('developers').appendNode('developer')
          developer.appendNode('id', 'sfeuerhahn')
          developer.appendNode('name', 'Stefan Feuerhahn')
        }
      }
    }
    repositories {
      maven {
        name 'bintray-juherr-asn1-jASN1'
        url 'https://api.bintray.com/maven/juherr/asn1/jASN1/'
        credentials {
          username = System.getenv('BINTRAY_USER')
          password = System.getenv('BINTRAY_API_KEY')
        }
      }
    }
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives sourcesJar
    archives javadocJar
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}
